import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

def gitSha() {
    return "git --git-dir=${rootDir}/.git --work-tree=${rootDir} rev-parse --short HEAD".execute().text.trim()
}

def buildTime() {
    def df = new SimpleDateFormat("ddMMyyyy")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def soundcloud = new Properties()
file("soundcloud.properties").withInputStream {
    stream -> soundcloud.load(stream)
}

sourceSets {
    unitTest {
        java.srcDir file('src/test/java')
        resources.srcDir file('src/test/res')
    }
}

configurations {
    unitTestCompile.extendsFrom runtime
    unitTestRuntime.extendsFrom unitTestCompile
}

android {
    compileSdkVersion 18
    buildToolsVersion "19"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 18

        versionCode 1
        versionName "0.9.0-${gitSha()}-${buildTime()}"

        buildConfig """\
            public static final String CLIENT_ID = \"${soundcloud.client_id}\";
            public static final String CLIENT_SECRET = \"${soundcloud.client_secret}\";
            public static final String VERSION_NAME = \"${versionName}\";
        """
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        release {
            // Make Android Studio stop complaining for null console object; Use this config from cmd
            if (System.console() != null) {
                storeFile file("release.keystore")
                keyAlias "soundscreen"
                // Without this ugly hack there is no installRelease task, so it is not possible to
                // build signed *-release.apk with custom readPasswordFromInput task
                storePassword ""
                keyPassword ""
            }
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix = "-DEBUG"
            signingConfig signingConfigs.debug
        }

        release {
            debuggable false
            runProguard true
            proguardFile file("proguard.cfg")
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        // alter the default packageApplication and outputFile to include custom versionName suffix
        def apk = variant.packageApplication.outputFile;
        def newName = apk.name.replace(".apk", "-${variant.mergedFlavor.versionName}.apk");
        variant.packageApplication.outputFile = new File(apk.parentFile, newName);
        if (variant.zipAlign) {
            variant.zipAlign.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
        }

        // print the APK path after assemble is done
        variant.assemble.doLast {
            println variant.outputFile
        }

        // print the APK path as the last step before install, at which point the apk is complete
        variant.install?.doFirst { // the install task can be null
            println variant.outputFile
        }
    }
}

task('readPasswordFromInput') << {
    android.signingConfigs.release {
        // NOTE: The System.console() call returns null if running using "gradle --daemon" for Java 6
        storePassword = new String(System.console().readPassword("\n\$ Enter keystore password: "))
        keyPassword = new String(System.console().readPassword("\n\$ Enter key password: "))
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.matches("validateReleaseSigning")) {
        task.dependsOn('readPasswordFromInput')
    }
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:19.0.+'

    compile 'com.soundcloud:java-api-wrapper:1.3.1'
    compile 'com.google.code.gson:gson:2.2.4'

    compile 'com.viewpagerindicator:library:2.4.1@aar'

    instrumentTestCompile 'com.squareup:fest-android:1.0.+'

    unitTestCompile files("$project.buildDir/classes/release")
    unitTestCompile 'junit:junit:4.10'
    unitTestCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'
    unitTestCompile 'com.squareup:fest-android:1.0.+'
    unitTestCompile 'com.google.android:android:4.0.1.2'
}

task unitTest(type:Test, dependsOn: assemble) {
    testClassesDir = project.sourceSets.unitTest.output.classesDir
    classpath = project.sourceSets.unitTest.runtimeClasspath
}
check.dependsOn unitTest