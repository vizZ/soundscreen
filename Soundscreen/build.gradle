buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "19"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 18

    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        release {
            // Make Android Studio stop complaining for null console object; Use this config from cmd
            if (System.console() != null) {
                storeFile file("release.keystore")
                keyAlias "soundscreen"
                // Without this ugly hack there is no installRelease task, so it is not possible to
                // build signed *-release.apk with custom readPasswordFromInput task
                storePassword ""
                keyPassword ""
            }
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix = "-DEBUG"
            signingConfig signingConfigs.debug
        }

        release {
            signingConfig signingConfigs.release
        }
    }
}

task('readPasswordFromInput') << {
    android.signingConfigs.release {
        storePassword = new String(System.console().readLine("\n\$ Enter keystore password: "))
        keyPassword = new String(System.console().readLine("\n\$ Enter key password: "))
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.matches("validateReleaseSigning")) {
        task.dependsOn('readPasswordFromInput')
    }
}

dependencies {
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:19.0.+'

    compile 'com.squareup.okhttp:okhttp:1.2.1'
    compile 'com.soundcloud:java-api-wrapper:1.3.1'
    compile 'com.google.code.gson:gson:2.2.4'

    compile 'com.viewpagerindicator:library:2.4.1@aar'

    instrumentTestCompile 'com.squareup:fest-android:1.0.+'
}
